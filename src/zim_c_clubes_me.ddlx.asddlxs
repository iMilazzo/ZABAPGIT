@Metadata.layer: #CUSTOMER
@UI:{

    headerInfo: {
        title: {
            type: #STANDARD,
            value: 'nome_time'
        },
        description: {
            value: 'nome_completo',
            type: #STANDARD
        },
        typeName: 'Clube',
        typeNamePlural: 'Clubes',
        imageUrl: 'LogoURL'
    },
    chart: [{
        chartType: #BULLET,
        measures:  [ 'pontos' ],
        measureAttributes: [{
            measure: 'pontos',
            asDataPoint: true,
            role: #AXIS_1
        }]
    },{
        chartType: #BULLET,
        measures:  [ 'kpi' ],
        measureAttributes: [{
            measure: 'kpi',
            asDataPoint: true,
            role: #AXIS_1
        }]
    }] 
}
//@UI: {
//    selectionPresentationVariant: [{
//        qualifier: 'KPIClube',
//        presentationVariantQualifier: 'KPIClube',
//        selectionVariantQualifier: 'KPIClube'
//    }],
//    presentationVariant: [{
//        qualifier: 'KPIClube',
//        text: 'KPI',
//        visualizations: [{
//            type: #AS_CHART,
//            qualifier: 'ChartKPI'
//            },{
//            type: #AS_DATAPOINT,
//            qualifier: 'KPIClube'
//            }]
//    }],
//    chart: [{  
//        qualifier: 'ChartKPI',
//        title: 'Gráfico do Aproveitamento',
//        chartType: #BULLET,
//        dimensions:  [ 'nome_time' ], 
//        measures:  [ 'kpi' ],
//        dimensionAttributes: [{
//            dimension: 'nome_time',
//            role: #CATEGORY
//        }],
//        measureAttributes: [{
//            measure: 'kpi',
//            role: #AXIS_1
//        }]
//    }]    
//}
annotate view ZIM_C_CLUBES
    with 
{

    @UI.facet: [
         { 
            id: 'header',
            purpose: #HEADER,
            type: #COLLECTION,
            targetQualifier: 'hdr'
         },
         { 
            label: 'Wiki',
            id: 'historico',
            purpose: #STANDARD,
            type: #COLLECTION,
            position: 10
         },
         { 
            label: 'Informações Gerais',
            id: 'GeneralInformation',
            purpose: #STANDARD,
            type: #COLLECTION,
            position: 20
         },
         { 
            label: 'Estatísticas',
            id: 'StatisticsInformation',
            purpose: #STANDARD,
            type: #COLLECTION,
            position: 30
         },
         { 
           id:'hdr',
           type: #FIELDGROUP_REFERENCE,
           purpose: #STANDARD,
           position: 10,
           parentId: 'header',
           targetQualifier: 'hdrInfo'
         },
         { 
           label: 'Elenco Atual',
           id:'elenco',
           type: #LINEITEM_REFERENCE,
           purpose: #STANDARD,
           position: 30,
           targetElement: '_Elenco'
         },
         { 
           label: 'Partidas',
           id:'items',
           type: #LINEITEM_REFERENCE,
           purpose: #STANDARD,
           position: 40,
           targetElement: '_Items'
         },
         {
           id:'historicoChild',
           purpose: #STANDARD,
           parentId: 'historico',
           type: #FIELDGROUP_REFERENCE,
           position: 10,
           targetQualifier: 'hist'
         },
         {
           label: 'Info',
           id:'info',
           purpose: #STANDARD,
           parentId: 'GeneralInformation',
           type: #FIELDGROUP_REFERENCE,
           position: 10,
           targetQualifier: 'one'
         },
         {
           label: 'Estádio',
           id:'estd',
           purpose: #STANDARD,
           parentId: 'GeneralInformation',
           type: #FIELDGROUP_REFERENCE,
           position: 20,
           targetQualifier: 'two'
         },
         {
           label: 'Admin',
           id:'admin',
           purpose: #STANDARD,
           parentId: 'GeneralInformation',
           type: #FIELDGROUP_REFERENCE,
           position: 30,
           targetQualifier: 'three'         
         },
         {
           label: 'Estatísticas',
           id:'stat',
           purpose: #STANDARD,
           parentId: 'StatisticsInformation',
           type: #DATAPOINT_REFERENCE,
           position: 40,
           targetQualifier: 'four'         
         }
    ]
    
    @UI: {
        multiLineText: true,
        fieldGroup: [{ qualifier: 'hdrInfo', position: 30, importance: #LOW }]
//        identification:[ { position: 10, label: '', importance: #LOW } ]
    }
    descricao;
    
    @UI: {
        fieldGroup: [{ qualifier: 'hdrInfo', position: 10, importance: #HIGH },
                     { qualifier: 'one', position: 10, importance: #HIGH }],
        lineItem: [ { position: 10, label: 'ID do Clube', importance: #HIGH } ]
//        identification:[ { valueQualifier: 'one', position: 10, label: 'ID do Clube' } ]
    }
    id_clube;

    @UI: {
        fieldGroup: [{ qualifier: 'one', position: 10, importance: #HIGH }],
        lineItem: [ { position: 20, label: 'Nome do Time', importance: #HIGH } ],
        selectionField: [ { position: 10 } ]
    }
    nome_time;


    @UI: {
        fieldGroup: [{ qualifier: 'one', position: 20, label: 'Nome Razão', importance: #LOW }]
//        identification:[ { position: 20, label: 'Nome Razão', importance: #LOW } ]
    }
    nome_completo;

    @UI: {
        fieldGroup: [{ position: 20, label: 'Estádio', importance: #MEDIUM, qualifier: 'two' },
                     { label: 'Estádio', type: #FOR_INTENT_BASED_NAVIGATION, semanticObjectAction: 'display' }],
        lineItem:  [{ position: 30, label: 'Estádio', importance: #MEDIUM },
                    { label: 'Estádio', type: #FOR_INTENT_BASED_NAVIGATION, semanticObjectAction: 'display' }]
    }
    id_estadio;
    
    @UI.hidden: true
    nome_estadio;

    @UI: {
        fieldGroup: [{ qualifier: 'two', position: 30, label: 'Fundado em', importance: #HIGH }],
        lineItem:  [{ position: 40, label: 'Fundado em' }]
    }
    fundacao;

    @UI: {
        fieldGroup: [{ qualifier: 'two', position: 40, label: 'Nome do Técnico', importance: #MEDIUM }]
    }
    nome_tecnico;


    @UI: {
        fieldGroup: [{ qualifier: 'three', position: 10, label: 'Criado em', importance: #LOW  }]
    }
    created_at;

    @UI: {
        fieldGroup: [{ qualifier: 'three', position: 20, label: 'Criado por', importance: #LOW }]
    }
    created_by;

    @UI: {
        fieldGroup: [{ qualifier: 'three', position: 30, label: 'Modificado em', importance: #LOW }]
    }
    changed_at;
      
    @UI: {
        fieldGroup: [{ qualifier: 'three', position: 40, label: 'Modificado por', importance: #LOW }]
    }
    changed_by;
    
    @UI: {
        fieldGroup: [{ qualifier: 'four', position: 50, label: 'Gols', importance: #LOW }]
    }
    gols;

    @UI: {
        dataPoint:{ 
            minimumValue: 0,
            maximumValue: 114,
            targetValue: 90,
            criticalityCalculation: {
                improvementDirection: #MAXIMIZE,
                deviationRangeLowValueElement: 'gols',
                toleranceRangeLowValue: 90
            },
            visualization: #BULLET_CHART,
            description: 'Pontos',
            title: 'PTO'
        },
        fieldGroup: [//{ position: 40, type: #AS_CHART, qualifier: 'hdrInfo', label: 'Pontuação', importance: #LOW },
                     { position: 60, type: #AS_CHART, qualifier: 'four', label: 'Pontuação', importance: #LOW }],
        lineItem:   [{ position: 50, type: #AS_CHART, label: 'Pontuação' }]
    }
    pontos;
    
    
    @UI: {
        dataPoint:{ 
            minimumValue: 0,
            maximumValue: 100,
            targetValue: 100,
            criticalityCalculation: {
                improvementDirection: #MAXIMIZE,
                deviationRangeLowValue: 20,
                toleranceRangeLowValue: 90
            },
            visualization: #PROGRESS,
            description: 'Aproveitamento do Clube',
            title: 'APR'
        },
        fieldGroup: [//{ position: 50, type: #AS_CHART, qualifier: 'hdrInfo', importance: #LOW },
                     { position: 70, type: #AS_DATAPOINT, qualifier: 'four', label: 'Aproveitamento', importance: #LOW }],
        lineItem:   [{ position: 60, type: #AS_DATAPOINT, label: 'Aproveitamento' }]
    }
    kpi;

}